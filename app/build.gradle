apply plugin: 'com.android.application'

def versionMajor = 1
def versionMinor = 1
def versionPatch = 27

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "pro.adamzielonka.converter"
        minSdkVersion 15
        targetSdkVersion 28
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        versionCode versionMajor * 10000 + versionMinor * 100 + versionPatch
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildTypes {
        debug {
            versionNameSuffix "-debug"
            resValue "string", "app_version", "${defaultConfig.versionName}${versionNameSuffix}"
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions "mode"
    productFlavors {
        alpha {
            dimension "mode"
            versionNameSuffix "-alpha"
            resValue 'string', 'app_version', "${defaultConfig.versionName}${versionNameSuffix}"
        }
        beta {
            dimension "mode"
            versionNameSuffix "-beta"
            resValue 'string', 'app_version', "${defaultConfig.versionName}${versionNameSuffix}"
        }
        noPrefix {
            dimension "mode"
            resValue 'string', 'app_version', "${defaultConfig.versionName}"
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.android.support:support-v4:28.0.0'
    implementation 'com.android.support:cardview-v7:28.0.0'
    implementation 'com.android.support:recyclerview-v7:28.0.0'
    implementation 'com.android.support:support-vector-drawable:28.0.0'
    implementation 'com.android.support:gridlayout-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation project(':items')
    implementation project(':java')
    implementation project(':verification')
    implementation project(':file')
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
